objref f2								// Define object reference for the cells2include file

objref celltypestring[1], cellType[1]	// Define placeholder objects with a size of one, then
										//  redefine them with the correct size in the proc

double cellnumvar[1], cellLayerflag[1], numCellTypes[1]	// Define placeholder doubles with a size
														//   of one, then redefine them with the
														//   correct size in the proc

proc loadCellCategoryInfo() {local i, startpos	// Load celltype info into a CellCategoryInfo
												//  object (one object per cell type)
	f2 = new File()
	f2.ropen("../cells/cells2include.hoc")		// Open the celltype
	
	numCellTypes = f2.scanvar			// Scan the first line, which contains a number giving the
										// # cell types, including stimulating (artificial) cells (still?)

	// Define variables to temporarily hold data scanned from file
	objref celltypestring[numCellTypes], cellType[numCellTypes]
	double cellnumvar[numCellTypes], cellLayerflag[numCellTypes]

// LEFT OFF HERE WITH FIXING THE DOCUMENTATION!!!


	for i=0, numCellTypes-1 {
		celltypestring[i]= new String()
		f2.scanstr(celltypestring[i].s)				// Scan in the cell name
		cellnumvar[i]=f2.scanvar					// Scan in the initial (before sclerosis) number of each cell type
		cellnumvar[i]=int(cellnumvar[i]/Scale)+1	// Account for the scaled model size
		cellLayerflag[i]=f2.scanvar					// Scan the layer flag (hilar=2, granular=1, molecular=0), where hilar cells
													//	are subject to sclerosis
	}
	f2.close()

	startpos=0
	for i=0, numCellTypes-1 {
	cellType[i] = new CellCategoryInfo(i)	// Make one object for each cell type to store cell type info
	cellType[i].setCellTypeParams(celltypestring[i].s, startpos, cellnumvar[i], cellLayerflag[i])	// Set parameters
	cellType[i].numCons = new Vector(numCellTypes,0)
	if (i>0) {cellType[i].setCellTypeDist}	// Load distribution file
	startpos = startpos + cellnumvar[i]		// Increment the startGid for the next cell type's call to setCellTypeParams
	}
}
loadCellCategoryInfo()
